
%{
#include<stdio.h>
#include "vector.h"
#include "macrovector.h"
#include "macro.h"
#include<string.h>
FILE *yyin;
void yyerror(char *message);
int max_args;
int i;
struct MacroArray* macros;
struct StringArray** list_of_expressions;
%}

%union{
	char *word;
	struct Macro* macro;
	struct StringArray* list_of_tokens;
	struct StringArray** inputs;
}


//Keywords
%token <word> CLASS
%token <word> PUBLIC
%token <word> STATIC
%token <word> VOID
%token <word> MAIN
%token <word> STRING
%token <word> SYSTEMOUTPRINTLN
%token <word> OUT
%token <word> PRINTLN
%token <word> EXTENDS
%token <word> RETURN
%token <word> INT
%token <word> IF 
%token <word> ELSE
%token <word> WHILE
%token <word> THIS
%token <word> NEW
%token <word> DEFINE
%token <word> LENGTH
%token <word> BOOLEAN
%token <word> HASH_DEFINE
%token <word> EOF_TOKEN
//Operators etc.,
%token <word> DOT
%token <word> COMMA
%token <word> SEMICOLON
%token <word> ASSIGNMENT 
%token <word> NOT
%token <word> OPERATOR

//Brackets
%token <word> LEFT_PARANTHESIS
%token <word> RIGHT_PARANTHESIS
%token <word> LEFT_BRACKET
%token <word> RIGHT_BRACKET
%token <word> LEFT_BRACE
%token <word> RIGHT_BRACE


%token <word> BOOLEAN_VALUE
%token <word> INTEGER
%token <word> IDENTIFIER

%type <macro> MacroDefStatement MacroDefExpression
%type <list_of_tokens> Expression PrimaryExpression 
%type <inputs> Expressions ExpressionsList
%type <list_of_tokens> IdentifiersList Identifiers Statements Statement Type
%start Goal 

%%

//MacroDefinition*
MacroDefinitions:
	       MacroDefinitions MacroDefinition
	       |
	       ;
	       
//TypeDeclaration*	       
TypeDeclarations:
		TypeDeclarations TypeDeclaration
		|
		;
		
		
Goal:
    MacroDefinitions MainClass TypeDeclarations 
    ;
    

MainClass:
	 CLASS IDENTIFIER LEFT_BRACE PUBLIC STATIC VOID MAIN LEFT_PARANTHESIS STRING LEFT_BRACKET RIGHT_BRACKET IDENTIFIER RIGHT_PARANTHESIS LEFT_BRACE SYSTEMOUTPRINTLN LEFT_PARANTHESIS Expression RIGHT_PARANTHESIS SEMICOLON RIGHT_BRACE RIGHT_BRACE
	 ;
	 
// (Type Identifier;)*
TypeIdentifierStatements:
		       TypeIdentifierStatements Type IDENTIFIER SEMICOLON 
		       |
		       ;

TypeDeclaration:
	       CLASS IDENTIFIER LEFT_BRACE TypeIdentifierStatements MethodDeclarations RIGHT_BRACE
	       | CLASS IDENTIFIER EXTENDS IDENTIFIER LEFT_BRACE TypeIdentifierStatements MethodDeclarations RIGHT_BRACE
	       ;
//Type Identifier (,Type Identifier)*
TypeIdentifiers:
	       TypeIdentifiers COMMA Type IDENTIFIER
	       | Type IDENTIFIER
	       ;

//((Type Identifier (,Type Identifier)*)?)
TypeIdentifiersList:
		   LEFT_PARANTHESIS TypeIdentifiers RIGHT_PARANTHESIS
		   | LEFT_PARANTHESIS RIGHT_PARANTHESIS
		   ;
//Expression (,Expression)*
Expressions:
	       Expressions COMMA Expression {
	       		$$ = $1;
	       		$$[i++] = $3;
	       }
	       | Expression {		
	       		if(list_of_expressions != NULL){
				free(list_of_expressions);
			}
			list_of_expressions = (struct StringArray**)malloc(max_args*sizeof(struct StringArray*));
			int j=0;
			for(; j<max_args; j++){
				list_of_expressions[j] = NULL;
			}
			i = 0;
			list_of_expressions[i++] = $1;
			
			$$ = list_of_expressions;
	       }
	       ;

//((Expression (,Expression)*)?)
ExpressionsList:
		   LEFT_PARANTHESIS Expressions RIGHT_PARANTHESIS {
		   	$$ = $2;
		   }
		   | LEFT_PARANTHESIS RIGHT_PARANTHESIS {
			int j=0;
			for(; j<max_args; j++){
				list_of_expressions[j] = NULL;
			}
		   }
		   ;

//Identifier (,Identifier)*
Identifiers:
	       Identifiers COMMA IDENTIFIER {
	       		$$ = $1;
			append($$, $3);	
	       }
	       | IDENTIFIER {
	       		$$  = new_string_array();
			append($$, $1);
	       }
	       ;

//((Identifier (,Identifier)*)?)
IdentifiersList:
		   LEFT_PARANTHESIS Identifiers RIGHT_PARANTHESIS{
		   	$$ = $2;
		   }
		   | LEFT_PARANTHESIS RIGHT_PARANTHESIS	   {
		   	$$ = new_string_array();
		   }
		   ;



MethodDeclaration:
		 PUBLIC Type IDENTIFIER TypeIdentifiersList LEFT_BRACE TypeIdentifierStatements Statements RETURN Expression SEMICOLON RIGHT_BRACE 
		;
//MethodDeclarations*
MethodDeclarations:
		  MethodDeclarations MethodDeclaration
		  | 
		  ;
//(Statement)*
Statements:
	  Statement Statements  {
	       		$$ = $1;
			cat($$, $2);
			free($2);
	       }
	  |{
		$$ = new_string_array();
	  }

	  ;
Type:
    INT LEFT_BRACKET RIGHT_BRACKET {
    	$$ = new_string_array();
	append($$, $1);
	append($$, $2);
	append($$, $3);
    }
    | BOOLEAN {
    	$$ = new_string_array();
	append($$, $1);
    }
    | INT {
    	$$ = new_string_array();
	append($$, $1);
    }
    | IDENTIFIER  {
    	$$ = new_string_array();
	append($$, $1);
    }
    ;

Statement:
	 LEFT_BRACE Statements RIGHT_BRACE{
	 	$$ = $2;
		insert($$, $1, 0);
	 	append($$, $3);
	 }
	 | SYSTEMOUTPRINTLN LEFT_PARANTHESIS Expression RIGHT_PARANTHESIS SEMICOLON{
	 	$$ = $3;
		insert($$, $2 ,0);
		insert($$, $1, 0);
		append($$, $4);
		append($$, $5);
	 }
	 | IDENTIFIER ASSIGNMENT  Expression SEMICOLON{
	 	$$ = $3;
		insert($$, $2, 0);
		insert($$, $1, 0);
		append($$, $4);
	 }
	 | IDENTIFIER LEFT_BRACKET Expression RIGHT_BRACKET ASSIGNMENT Expression SEMICOLON {
	 	$$ = $3;	 
		insert($$, $2, 0);
		insert($$, $1, 0);
		append($$, $4);
		append($$, $5);
		cat($$, $6);
		append($$,$7);
		free($6);
	 }
	 | IF LEFT_PARANTHESIS Expression RIGHT_PARANTHESIS Statement {
	 	$$ = $3;
		insert($$, $2, 0);
		insert($$, $1, 0);
		append($$, $4);
		cat($$, $5);
	 }
	 | IF LEFT_PARANTHESIS Expression RIGHT_PARANTHESIS Statement ELSE Statement{
	 	$$ = $3;
		insert($$, $2, 0);
		insert($$, $1, 0);
		append($$, $4);
		cat($$, $5);
	 	append($$, $6);
	 	cat($$, $7);
		free($5);
		free($7);
	 }
	 | WHILE LEFT_PARANTHESIS Expression RIGHT_PARANTHESIS Statement{
	 	$$ = $3;;
		insert($$, $2, 0);
		insert($$, $1, 0);
		append($$, $4);
		cat($$, $5);
		free($5);
	 }
	 | IDENTIFIER ExpressionsList SEMICOLON{
	 	//macro statement
		 struct Macro* sub = macro_locate(macros, $1);
		 if(sub == NULL){
		 	yyerror("Macro not found");
		 }else{
		 	struct StringArray* result = substitute(sub, $2);
			//struct StringArray* result = new_string_array();
			$$ = result;
			//append($$, "MACRO");
			//$$ = result;
			append($$,$3);
			i = 0;
		 }
	 }
	 ;

Expression:
	  PrimaryExpression OPERATOR PrimaryExpression {
	  	$$ = $1;
		append($$, $2);
		cat($$, $3);
		free($3);
	  }
	  | PrimaryExpression LEFT_BRACKET PrimaryExpression RIGHT_BRACKET{
	  	$$ = $1;
		append($$, $2);
		cat($$, $3);
	  	append($$, $4);
		free($3);
	  } 
	  | PrimaryExpression DOT LENGTH{
	  	$$ = $1;
		append($$, $2);
		append($$, $3);
		
	  }
	  | PrimaryExpression{
	  	$$ = $1;
	  }
	  | PrimaryExpression DOT IDENTIFIER ExpressionsList{
	  	$$ = $1;
		append($$, $2);
		append($$, $3);
		//Convert expression list to a single array
		int j=0;
		append($$, "(");
		for(; j<i; j++){
			cat($$, $4[j]);
			if(j+1<i){
				append($$,",");
			}
		}
		append($$, ")");
		i = 0;
		
	  }
	  | IDENTIFIER ExpressionsList{
	  	 struct Macro* sub = macro_locate(macros, $1);
		 if(sub == NULL){
		 	yyerror("Macro not found");
		 }else{
			//print_a_macro(sub);
		 	struct StringArray* result = substitute(sub, $2);
			//struct StringArray* result = new_string_array();
			$$ = result;
			//append($$, "MACRO");
			//print($$);
			i = 0;
		 }
		
	 	 //macro statement call	
	  }
	  ;
PrimaryExpression:
		 INTEGER {
		 	$$ = new_string_array();
			append($$, $1);
		 }
		 | BOOLEAN_VALUE {
		 	$$ = new_string_array();
			append($$, $1);
		 }
		 | IDENTIFIER {
		 	$$ = new_string_array();
			append($$, $1);
		 }
		 | THIS {
		 	$$ = new_string_array();
			append($$, $1);
		 }
		 | NEW INT LEFT_BRACKET Expression RIGHT_BRACKET{
		 	$$ = new_string_array();
			append($$, $1);
			append($$, $2);
			append($$, $3);
			cat($$, $4);
			append($$, $5);
			free($4);
		 }
		 | NEW IDENTIFIER LEFT_PARANTHESIS RIGHT_PARANTHESIS{
		 	$$ = new_string_array();
			append($$, $1);
			append($$, $2);
			append($$, $3);
			append($$, $4);
			
		 }
		 | NOT Expression{
		 	$$ = new_string_array();
			append($$, $1);
			cat($$, $2);
			free($2);
		 }
		 | LEFT_PARANTHESIS Expression RIGHT_PARANTHESIS{
		 	$$ = new_string_array();
			append($$, $1);
			cat($$, $2);
			append($$, $3);
			free($2);
		 }
		 ;
MacroDefinition:
	       MacroDefExpression
	       | MacroDefStatement
	       ;

MacroDefStatement:
		HASH_DEFINE IDENTIFIER IdentifiersList LEFT_BRACE Statements RIGHT_BRACE{
			$$ = new_macro();
			$$->name = $2;
			$$->args = $3;
			$$->expr = $4;
			macro_append(macros, $$);
			if($$->args->size > max_args){
				max_args = $$->args->size;
			}
		}
		 ;



MacroDefExpression:
		 HASH_DEFINE  IDENTIFIER IdentifiersList LEFT_PARANTHESIS Expression RIGHT_PARANTHESIS{
		 	$$ = new_macro();
			$$->name = $2;
			$$->args = $3; 
			$$->expr = $5;
			macro_append(macros, $$);
			if($$->args->size > max_args){
				max_args = $$->args->size;
			}
		 }
		 ;





%%

int main(){
	max_args = 0;
	list_of_expressions = NULL;
	i = 0;
	macros = new_macro_array(); 
	yyparse();
}



void yyerror(char *message) {
	printf("ERROR PARSING : %s\n",message);	
	exit(-1);
}
